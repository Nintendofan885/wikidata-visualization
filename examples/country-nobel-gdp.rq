# Country comparison of number of Nobel laureates per capita and GDP per capita

SELECT ?countryName
       # number of Nobel laureates per capita
       (COUNT(*) / AVG(?population) AS ?numOfLaureatesPerCapita)
       # GDP per capita
       (AVG(?GDPPerCapita) AS ?GDPPerCapita)
WHERE {
  ?personItem wdt:P31 wd:Q5 ; # human
              wdt:P166/wdt:P31 wd:Q7191 ; # Nobel Prize
              wdt:P27 ?countryItem .
  ?countryItem wdt:P2132 ?GDPPerCapita ;
               wdt:P1082 ?population .
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en" .
                           ?countryItem rdfs:label ?countryName }
}
GROUP BY ?countryName
