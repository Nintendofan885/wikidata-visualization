# Number of National Historical and Cultural Sites in each province of China

SELECT ?provinceItem ?province (COUNT(*) AS ?numOfSites) ?provinceCoord WHERE {
  hint:Query hint:optimizer "None" .
  ?siteItem wdt:P1435 wd:Q1188574 ; # Chinese National Historical and Cultural Site
                wdt:P131+ ?provinceItem .
  ?provinceItem wdt:P31/wdt:P279 wd:Q13396669 . # province-level division in China
  OPTIONAL { ?provinceItem wdt:P625 ?provinceCoord . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "zh" .
                           ?provinceItem rdfs:label ?province . }
}
GROUP BY ?provinceItem ?province ?provinceCoord
ORDER BY DESC(?numOfSites)
